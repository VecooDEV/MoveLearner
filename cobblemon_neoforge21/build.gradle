plugins {
    id 'net.neoforged.moddev' version '2.0.124'
    id 'com.modrinth.minotaur' version '2.8.10'
    id 'org.jetbrains.kotlin.jvm' version '1.9.24'
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

version = "${project_version}-1.21.1-Cobblemon-NeoForge"

neoForge {
    version = "${neoforge_21}"
}

dependencies {
    compileOnly "maven.modrinth:cobblemon:${cobblemon_21}-neoforge,1.21.1"
    compileOnly "maven.modrinth:impactor:${impactor_21}-Neoforge-neoforge,1.21.1"
    compileOnly "maven.modrinth:cobbledollars:${cobbledollars_21}-neoforge,1.21.1"
    compileOnly "maven.modrinth:extralib:${extralib}-neoforge,1.21.1"
}

processResources {
    filesMatching('META-INF/neoforge.mods.toml') {
        expand(
                'project_name': project_name,
                'project_version': project_version
        )
    }
}

def gradleProperties = new Properties()
def gradlePropertiesFile = new File(rootProject.file("/.gradle/gradle.properties").path)

if (gradlePropertiesFile.exists()) {
    gradlePropertiesFile.withInputStream { gradleProperties.load(it) }
}

modrinth {
    token = gradleProperties.getProperty("MODRINTH_API_KEY")
    projectId = "movelearner"
    versionName = "${project_name}-${version}"
    versionNumber = project_version
    versionType = "release"
    changelog = rootProject.file("changelog.md").text
    uploadFile = jar
    gameVersions = ["1.21.1"]
    loaders = ["neoforge"]
    dependencies {
        required.project "cobblemon"
        required.project "extralib"
        optional.project "cobbledollars"
        optional.project "impactor"
    }
}

build.finalizedBy('versionedRelease')