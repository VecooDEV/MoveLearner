plugins {
    id 'fabric-loom' version '1.13+'
    id 'com.modrinth.minotaur' version '2.8.10'
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

archivesBaseName = "${project_name}"

version = "${project_version}-1.21.1-Cobblemon-Fabric"

dependencies {
    minecraft "com.mojang:minecraft:1.21.1"
    mappings loom.officialMojangMappings()

    modCompileOnly "net.fabricmc:fabric-loader:${fabric_loader}"
    modCompileOnly "net.fabricmc.fabric-api:fabric-api:${fabric_api_21}"
    modCompileOnly "net.fabricmc:fabric-language-kotlin:${project.fabric_kotlin}"
    modCompileOnly "maven.modrinth:cobblemon:${cobblemon_21}-fabric,1.21.1"
    modCompileOnly "maven.modrinth:extralib:${extralib}-fabric,1.21.1"
}

processResources {
    filesMatching('fabric.mod.json') {
        expand(
                'project_name': project_name,
                'project_version': project_version
        )
    }
}

remapJar {}

def gradleProperties = new Properties()
def gradlePropertiesFile = new File(rootProject.file("/.gradle/gradle.properties").path)

if (gradlePropertiesFile.exists()) {
    gradlePropertiesFile.withInputStream { gradleProperties.load(it) }
}

modrinth {
    token = gradleProperties.getProperty("MODRINTH_API_KEY")
    projectId = "movelearner"
    versionName = "${project_name}-${version}"
    versionNumber = project_version
    versionType = "release"
    changelog = rootProject.file("changelog.md").text
    uploadFile = remapJar
    gameVersions = ["1.21.1"]
    loaders = ["fabric"]
    dependencies {
        required.project "cobblemon"
        required.project "extralib"
    }
}

build.finalizedBy('versionedRelease')